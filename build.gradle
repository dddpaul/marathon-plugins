buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
}

plugins {
    id 'com.gradle.build-scan' version '2.1'
    id 'io.franzbecker.gradle-lombok' version '1.14'
    id 'com.github.johnrengelman.shadow' version '4.0.3'
    id "com.jfrog.bintray" version "1.8.4"
    id 'java'
    id 'maven-publish'
    id 'jacoco'
}

ext {
    scalaVersion = project.hasProperty('scalaVersion') ? project.getProperty('scalaVersion') : '2.11'
    marathonVersion = project.hasProperty('marathonVersion') ? project.getProperty('marathonVersion') : '1.5.12'
    junitVersion = '5.3.1'
}

group = 'com.github.dddpaul.marathon'
version = "0.1.0"
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenLocal()
    jcenter()
    mavenCentral()
    maven {
        url "http://downloads.mesosphere.io/maven"
    }
}

dependencies {
    compile 'commons-codec:commons-codec:1.11'
    compile 'org.apache.commons:commons-lang3:3.8.1'
    compile 'at.favre.lib:bcrypt:0.6.0'

    compileOnly "mesosphere.marathon:marathon_$scalaVersion:$marathonVersion"
    compileOnly "mesosphere.marathon:plugin-interface_$scalaVersion:$marathonVersion"

    testCompile "mesosphere.marathon:marathon_$scalaVersion:$marathonVersion"
    testCompile "mesosphere.marathon:plugin-interface_$scalaVersion:$marathonVersion"
    testCompile 'org.testcontainers:testcontainers:1.9.1'
    testCompile 'com.squareup.okhttp3:okhttp:3.11.0'
    testCompile "org.junit.jupiter:junit-jupiter-api:$junitVersion"
    testCompile "org.junit.jupiter:junit-jupiter-params:$junitVersion"
    testRuntime "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
}

// Shade up compile-type dependencies to prevent clashing with Marathon-bundled libraries
shadowJar {
    relocate 'org.apache.commons', 'com.github.dddpaul.shaded.org.apache.commons'
    relocate 'at.favre', 'com.github.dddpaul.shaded.at.favre'
    minimize()
}

test {
    useJUnitPlatform()
}

wrapper {
    gradleVersion = '5.0'
    distributionUrl = 'https://services.gradle.org/distributions/gradle-5.0-all.zip'
}

buildScan {
    termsOfServiceUrl = 'https://gradle.com/terms-of-service'
    termsOfServiceAgree = 'yes'
}

lombok {
    version = '1.18.4'
    sha256 = ""
}


bintray {
    user = project.hasProperty('bintray_user') ? project.getProperty('bintray_user') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintray_key') ? project.getProperty('bintray_key') : System.getenv('BINTRAY_KEY')
    publications = ['shadow']
    publish = true

    pkg {
        repo = 'maven'
        name = "$project.name"
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/dddpaul/marathon-plugins-auth.git'
    }
}

publishing {
    publications {
        shadow(MavenPublication) { publication ->
            project.shadow.component(publication)
        }
    }
}
